import proguard.gradle.ProGuardTask

plugins {
    id 'fabric-loom'
}

group = 'io.layux.example'
description = 'Example Fabric mod'
ext.mod_id = 'fabric-example'
ext.mod_name = 'Fabric Example'

dependencies {
    // External dependencies
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings_version:v2"
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_language_kotlin_version"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "$mod_id" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=$mod_id"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    // Add the generated resources to the main source set
    main {
        resources {
            srcDirs += ['src/main/generated']
        }
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

processResources {
    filesMatching('fabric.mod.json') {
        expand([
                mod_id: project.mod_id,
                mod_name: project.mod_name,
                description: project.description,
                version: project.version,
                fabric_loader: "$fabric_loader_version",
                fabric_api: "$fabric_version",
                fabric_language_kotlin: "$fabric_language_kotlin_version",
                minecraft: "$minecraft_version",
                java: "$java_version"
        ])
    }
}

// Loom generates a shaded jar by default, however we still want to obfuscate it for
// distribution. This task will run ProGuard on the shaded jar and replace it with the
// obfuscated one.
def jarFile = remapJar.outputs.files.singleFile
def proguardJar = file("$buildDir/libs/${project.name}-proguard.jar")

tasks.register('proguard', ProGuardTask) {
    dependsOn remapJar

    group = 'build'
    description = 'Runs ProGuard on the shaded jar'

    println "ProGuarding ${jarFile.name} to ${proguardJar.name}"

    outputs.upToDateWhen { false }
    configuration rootProject.file('.proguard/fabric.pro')

    injars jarFile
    outjars proguardJar
    // Include all runtime dependencies
    libraryjars configurations.findByName('runtimeClasspath').getFiles()
    // Java 8
    libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    // Java 9+ compatibility
    libraryjars "${System.getProperty("java.home")}/jmods/java.base.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.desktop.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.logging.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/jdk.unsupported.jmod"

    doLast {
        //if (!jarFile.delete())
        //    throw new IllegalStateException("Failed to delete ${jarFile.name}")

        //if (!proguardJar.renameTo(jarFile))
        //    throw new IllegalStateException("Failed to rename ${proguardJar.name} to ${jarFile.name}")
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            from components.java

            group = project.group
            version = project.version
        }
    }
}
