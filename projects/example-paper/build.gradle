import net.minecrell.pluginyml.bukkit.BukkitPluginDescription
import proguard.gradle.ProGuardTask

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'io.papermc.paperweight.userdev'
    id 'net.minecrell.plugin-yml.bukkit'
    id 'org.jetbrains.kotlin.kapt'
    id 'xyz.jpenilla.run-paper'
}

group = 'io.layux.example'
description = 'Example Paper plugin'
ext.plugin_name = 'ExamplePlugin'

dependencies {
    // Dependency injection
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // External dependencies
    paperweightDevelopmentBundle "io.papermc.paper:dev-bundle:$paper_api_version"
}

assemble {
    dependsOn reobfJar
}

bukkit {
    name = "${project.plugin_name}"
    version = project.version
    description = project.description
    load = BukkitPluginDescription.PluginLoadOrder.STARTUP
    main = "${project.group}.${project.plugin_name}"
    apiVersion = "1.20"
    authors = ["Layux"]
}

// Shadow the jar to include all runtime dependencies
shadowJar {
    archiveClassifier.set('')
}

jar.finalizedBy shadowJar

// Obfuscate the shaded jar using ProGuard
def jarFile = shadowJar.outputs.files.singleFile
def proguardJar = file("$jarFile-proguard.jar")

println "Running ProGuard on ${jarFile} to ${proguardJar}"

tasks.register('proguard', ProGuardTask) {
    dependsOn shadowJar

    group = 'build'
    description = 'Runs ProGuard on the shaded jar'

    outputs.upToDateWhen { false }
    configuration rootProject.file('.proguard/paper.pro')

    injars jarFile
    outjars proguardJar
    // Include all runtime dependencies
    libraryjars configurations.findByName('runtimeClasspath').getFiles()
    // Java 8
    libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    // Java 9+ compatibility
    libraryjars "${System.getProperty("java.home")}/jmods/java.base.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.desktop.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.logging.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/jdk.unsupported.jmod"

    doLast {
        if (!jarFile.delete())
            throw new IllegalStateException("Failed to delete ${jarFile.name}")

        if (!proguardJar.renameTo(jarFile))
            throw new IllegalStateException("Failed to rename ${proguardJar.name} to ${finalJar.name}")
    }
}

// Publish the obfuscated jar to our artifact repository
publishing {
    publications {
        gpr(MavenPublication) {
            artifact jarFile
            group = project.group
            version = project.version
        }
    }
}

// Run the server
runServer {
    pluginJars.from jarFile
}
